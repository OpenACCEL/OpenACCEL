 
dAction=slider(0.073,-0.1,0.1)
    //  the relative contribution of differentiating action 
kAction=slider(0.22,0,1)
    //  the overall strength of the control feedback loop 
noise=slider(0.23,0,1)
    //  the amplitude of the random perturbation 
pAction=slider(0.0029,-0.01,0.01)
    //  the relative contribution of proportional action 
showGraph=check(false)
    //  to determine if the graph should be visible 
cy=cursorY() 
cx=cursorX() 
spar=cond(abs(cy-y0)<10,cx-50,spar{1})
    //  the set-point (=the position of the red dot) 
tBut=button() 
time=cond(tBut,0,time{1}+1)
    //  resetting the time and incrementing the time
plotStick=[[x:[mode:'data',ref:1],y:[mode:'data',ref:2],col_r:0,col_g:255,col_b:0,width:5],[x1,x2],[y1,y2]]
    // this represents the tilting stick (actually a slide)
plotPhi=[[x:[mode:'intp'],y:[mode:'shift',ref:1],width:3,col_b:255,col_r:0],50+50*[showphi]]
    // this represents the angle phi as a function of time
plotBall=[[plotType:'bubble',x:[mode:'shift',ref:1],y:[mode:'shift',ref:2],diameter:[mode:'data',ref:3]],[rx],[ry],[d]]
    // this is the rolling ball
plotAnchor=[[plotType:'bubble',x:[mode:'shift',ref:1],y:[mode:'shift',ref:2],diameter:[mode:'data',ref:3],col_r:255,col_g:0],[sx],[sy],[3]]
    // this is the little anchor point, to be moved by the user (the set point of the controller)
plotResult=plot([plotPhi,plotStick,plotBall,plotAnchor])
    //  three graphs:
    //  the green line segment representing the stick,
    //  the big green bubble representing the ball,
    //  the blue curve: the variation of phi 
    //  four graphs:
    //  the green line segment representing the stick,
    //  the big green bubble representing the ball,
    //  the small magenta bubble representing the set point
    //  the cyan curve representing the controlled angle phi 
Rmax=40
    //  determines the length of the stick 
d=10
    //  the diameter of the ball 
g=0.5
    //  the gravity 
x0=50
    //  the hinge, x-coordinate 
y0=40
    //  the hinge, y-coordinate 
cphi=cos(phi)
    //  so we don't have to calculate the cos every time 
ctrlPhi=phi{1}*(1-kAction)+kAction*(pAction*(rpar{1}-spar)+dAction*(rpar{1}-rpar{2}))
    //  the angle of the stick as a result of the feedback controllers
    //  the first term is the own (previous) value
    //  the second term is the proportional action: propoertional to the difference between the soll and ist-werte
    //  the third term is the differentiating action: proportional to the first derivative 
gpar=-g*sphi
    //  the parallel component of gravity 
phi=cond(time>1,noise*(random()-0.5)+ctrlPhi,0.05)
    //  the actual angle of the stick: the controlled angle plus a noise contribution 
rpar=cond(time>1,rpar{1}+vpar,0)
    //  the parallel displacement: integrate over the parallel velocity 
rx=x0+d*sphi/2+rpar*cphi
    //  the x-coordinate of the ball 
ry=y0+d*cphi/2+rpar*sphi
    //  the y-coordinate of the ball 
showphi=cond(showGraph,phi,-1)
    //  if the graph should be shown, showPhi equals the value of phi 
sphi=sin(phi)
    //  so we don't have to calculate the sine every time over again 
sx=x0+spar*cphi
    //  the x-coordinate of the set point (=the red dot) 
sy=y0+spar*sphi
    //  the y-coordinate of the set point (=the red dot) 
vpar=cond(time>1,vpar{1}+gpar,0)
    //  the parallel component of the velocity: integrating the acceleration 
x1=x0+Rmax*cphi
    //  x coordinate of one end point of the stick 
x2=x0-Rmax*cphi
    //  x coordinate of other end point of the stick 
y1=y0+Rmax*sphi
    //  y coordinate of one end point of the stick 
y2=y0-Rmax*sphi
    //  y coordinate of other end point of the stick
    // [['n':'dAction','x':20.4,'y':86.6],['n':'kAction','x':7.4,'y':88.6],['n':'noise','x':11.5,'y':91.4],['n':'pAction','x':16.2,'y':89.4],['n':'showGraph','x':16,'y':94.5],['n':'cy','x':6.2,'y':94.5],['n':'cx','x':23.8,'y':90.3],['n':'spar','x':30.4,'y':52.8],['n':'y0','x':10.4,'y':12.7],['n':'tBut','x':21.1,'y':94.7],['n':'time','x':38.5,'y':68.3],['n':'plotResult','x':93.2,'y':59.4],['n':'x1','x':78.4,'y':30],['n':'x2','x':63.5,'y':55.5],['n':'y1','x':58.5,'y':56.1],['n':'y2','x':87.9,'y':28],['n':'rx','x':69,'y':53],['n':'ry','x':67.4,'y':48.2],['n':'d','x':5.9,'y':10.4],['n':'sx','x':75.6,'y':36.4],['n':'sy','x':66.7,'y':39.3],['n':'showphi','x':61.9,'y':45.8],['n':'Rmax','x':19.8,'y':5],['n':'g','x':13.8,'y':5.3],['n':'x0','x':14.8,'y':10.4],['n':'cphi','x':54.9,'y':50.5],['n':'phi','x':39,'y':53.8],['n':'ctrlPhi','x':38.1,'y':60.2],['n':'rpar','x':63.6,'y':65.8],['n':'gpar','x':43.3,'y':49],['n':'sphi','x':42,'y':44.2],['n':'vpar','x':60.1,'y':61]]