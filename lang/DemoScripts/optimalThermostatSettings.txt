// Version with ideal heater

// Sampling
NOS = 100
rangeOfSamples = vSeq(0,NOS)

// Initial Values
initialRoomTemp = 14
maxOutsideTemp = 10
minOutsideTemp = -5

// Create outside temp function
ampOutsideTemp = (maxOutsideTemp - minOutsideTemp) /2
midOutsideTemp = (maxOutsideTemp + minOutsideTemp) /2
outsideTemp(t) = midOutsideTemp - ampOutsideTemp*cos(t*2*PI/NOS)

// Create desired temp function
desiredNightTemp = 14
desiredDayTemp = 20
desiredTemp(t) = vVecRamp([0.375*NOS,0.375*NOS+1,0.916*NOS,0.916*NOS+1],[desiredNightTemp,desiredDayTemp,desiredDayTemp,desiredNightTemp],t)

// Room Temp
tempStep = 0.1
defaultTemp = 17/tempStep
maxTemp = 25/tempStep
//Temp0=slider(defaultTemp,0,maxTemp)
Temp0=slider(170,120,230)
Temp1=slider(170,120,230)
Temp2=slider(170,120,230)
Temp3=slider(170,120,230)
Temp4=slider(170,120,230)
Temp5=slider(170,120,230)
Temp6=slider(170,120,230)
Temp7=slider(170,120,230)
Temp8=slider(170,120,230)
Temp9=slider(170,120,230)
T0 = Temp0*tempStep
T1 = Temp1*tempStep
T2 = Temp2*tempStep
T3 = Temp3*tempStep
T4 = Temp4*tempStep
T5 = Temp5*tempStep
T6 = Temp6*tempStep
T7 = Temp7*tempStep
T8 = Temp8*tempStep
T9 = Temp9*tempStep
roomTemp(t) = vVecRamp([0,0.15*NOS,0.28*NOS,0.35*NOS,0.4*NOS,0.5*NOS,0.8*NOS,0.85*NOS,0.9*NOS,0.95*NOS,NOS],[T0,T1,T2,T3,T4,T5,T6,T7,T8,T9,T0],t)

// Heatloss and insulation values
insulationK = 0.04
insulationA = 158
insulationThickness = 2
secondsPerSample = 24*60*60/NOS
heatLoss = #(i,rangeOfSamples+1,max(0,insulationK*insulationA*(roomTemp(i) - outsideTemp(i))/insulationThickness*secondsPerSample),add)

// Heating
heatCapacityAir = 1005
massAir = 155.16
heating = #(i,rangeOfSamples+1,max(0,(roomTemp(i)-roomTemp(i-1))*heatCapacityAir*massAir),add)

// Totals
heatTot = heatLoss+heating
discomfortScaling = 3
lackingComfort = #(i,rangeOfSamples,pow(discomfortScaling,abs(desiredTemp(i) - roomTemp(i))), add)
costs = paretoVer(paretoMin(heatTot/1000))
discomfort = paretoHor(paretoMin(lackingComfort))

// Plotting
desiredTempData = #(i,rangeOfSamples,desiredTemp(i),vAppend)
desiredTempGraph = [edges:[col_g:255],locations:[x:[mode:'intp'],data:#(i,rangeOfSamples,[y:desiredTempData[i]],vAppend)]]
roomTempData = #(i,rangeOfSamples,roomTemp(i),vAppend)
roomTempGraph = [edges:[col_r:255],locations:[x:[mode:'intp'],data:#(i,rangeOfSamples,[y:roomTempData[i]],vAppend)]]
outsideTempData = #(i,rangeOfSamples,outsideTemp(i),vAppend)
outsideTempGraph = [edges:[col_b:255],locations:[x:[mode:'intp'],data:#(i,rangeOfSamples,[y:outsideTempData[i]],vAppend)]]
graphs = descartes([desiredTempGraph,roomTempGraph,outsideTempGraph])
